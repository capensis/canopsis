(* KEYWORDS *)

@@keyword :: ALL
@@keyword :: TRUE FALSE NULL
@@keyword :: NOT AND OR
@@keyword :: CONTAINS LIKE

(*/KEYWORDS *)

(* TYPES *)
true::true = "TRUE" ;
false::false = "FALSE" ;
digits::digits = value:/[0-9]+/ ;
characters::characters = value:/[\-_a-zA-Z][\-_a-zA-Z0-9]*(\.[\-_a-zA-Z][\-_a-zA-Z0-9]*)*/ ;
sign::sign = value:( "+" | "-" ) ;

string::forward_value = '"' value:characters '"' | "'" value:characters "'" ;
integer::integer = [ sign:sign ] value:digits ;
floating::floating = [ sign:sign ] intpart:digits "." floatpart:digits ;
boolean::forward_value = value:( true | false ) ;
none::none = "NULL";
(*/TYPES *)

(* GENERIC *)
@name
key::key = value:( /[_a-zA-Z][_a-zA-Z0-9]*(\.[_a-zA-Z][_a-zA-Z0-9]*)*/ ) ;
value::forward_value = value:( string | floating | integer | boolean | none ) ;
not::not = "NOT" ;
(*/GENERIC *)

(* CONDITION *)
compop::compop = value:( "<=" | "<" | "=" | "!=" | ">=" | ">" | "CONTAINS" | "LIKE" ) ;
comparison::comparison = [ not:not ] left:key compop:compop right:value ;

condop::condop = value:( "AND" | "OR" ) ;
next_condition::next_condition = condop:condop right:comparison ;

condition::condition = left:comparison { next:next_condition }* ;
(*/CONDITION *)

(* SEARCH *)
search::search = [ scope:( "ALL" ) ] condition:condition $ ;
(*/SEARCH *)

start = search:search ;
